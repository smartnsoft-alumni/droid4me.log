apply plugin: "com.android.library"
apply plugin: "com.jfrog.bintray"
apply plugin: "maven"
apply plugin: "maven-publish"

ext
{
  droid4meVersion="2.5.2"
}

android
{
  compileSdkVersion 25
  buildToolsVersion "25.0.3"

  defaultConfig
  {
    minSdkVersion 15
    targetSdkVersion 25

    versionCode 3
    versionName "4.4.1.3"
  }

  buildTypes
  {
    release
    {
      minifyEnabled false
      zipAlignEnabled true
      proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
    }
  }
}

dependencies
{
  // Smart&Soft
  compile("com.smartnsoft:droid4me:$droid4meVersion")

  //Third party library
  compile ("com.github.LogentriesCommunity:le_android:logentries-android-4.4.1")
}

task sourcesJar(type: Jar) {
  classifier = "sources"
  from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
  title = "logentries implementation API"
  description "Javadoc for the logentries logger implementation"
  source = android.sourceSets.main.java.srcDirs

  options.charSet = "UTF-8"
  options.overview = "src/main/javadoc/overview.html"
  options.links = ["http://docs.oracle.com/javase/7/docs/api/", "http://d.android.com/reference/"]

  classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
  failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = "javadoc"
  from javadoc.destinationDir
  // options.encoding = 'UTF-8'
}

artifacts
{
  archives javadocJar
  archives sourcesJar
}

def pomConfig = {
  licenses
  {
    license
    {
      name "MIT License (MIT)"
      url "https://opensource.org/licenses/MIT"
      distribution "repo"
    }
  }
}

publishing
{
  publications
  {
    ReleasePublication(MavenPublication)
    {
      artifact sourcesJar
      artifact javadocJar
      groupId "com.smartnsoft"
      artifactId "logentries"
      version android.defaultConfig.versionName

      pom.withXml
      {
        def root = asNode()
        root.appendNode("description", "extension of the droid4me framework which contains logentries logger implementation")
        root.appendNode("name", "logentries")
        root.appendNode("url", "https://github.com/smartnsoft/droid4me.log")
        root.children().last() + pomConfig
      }
    }
  }
}

//https://gist.github.com/bugs84/b7887fb5d7f9f2d484b8
publishing.publications.all
{
  pom.withXml
  {
    asNode().dependencies."*".findAll()
    {
      it.scope.text() == "runtime" && project.configurations.compile.allDependencies.find { dep ->
        dep.name == it.artifactId.text()
      }
    }.each
    {
      it.scope*.value = "compile"
    }
  }
}

bintray
{
  user = bintrayUsername
  key = bintrayKey

  publications = ["ReleasePublication"]

  pkg
  {
    repo = "maven"
    name = "logentries"
    userOrg = "smartnsoft"
    licenses = ["MIT"]
    vcsUrl = "extension of the droid4me framework which contains logentries logger implementation"
    publish = true

    version
    {
      name = android.defaultConfig.versionName
      desc = "extension of the droid4me framework which contains logentries logger implementation"
      released  = new Date()
      vcsTag = "logentries-" + android.defaultConfig.versionName
    }
  }
}

uploadArchives
{
  repositories
  {
    mavenDeployer
    {
      repository(url: "https://api.bintray.com/maven/smartnsoft/maven/logentries/;publish=1")
      {
        authentication(userName: bintrayUsername, password: bintrayKey)
      }

      snapshotRepository(url: nexusSnapshotUrl)
      {
        authentication(userName: nexusUsername, password: nexusPassword)
      }

      pom.project
      {
        name "logentries"
        groupId "com.smartnsoft"
        artifactId "logentries"
        version android.defaultConfig.versionName
        packaging "aar"
      }
    }
  }
}